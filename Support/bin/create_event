#!/usr/bin/osascript

on getDateFromString(dateText)
	set AppleScript's text item delimiters to {" "}
	set theDay to text item 1 of dateText
	set theTime to text item 2 of dateText

	set AppleScript's text item delimiters to {"-"}
	set theYear to text item 1 of theDay
	set theMonth to text item 2 of theDay
	set theDay to text item 3 of theDay

	set AppleScript's text item delimiters to {":"}
	set theHour to text item 1 of theTime
	set theMinute to text item 2 of theTime
	set theSecond to text item 3 of theTime

	set theDate to current date
	set year of theDate to (theYear as integer)
	set month of theDate to theMonth as integer
	set day of theDate to theDay as integer
	set hours of theDate to theHour as integer
	set minutes of theDate to theMinute as integer
	set seconds of theDate to theSecond as integer
	return theDate
end getDateFromString

on run argv
	try

		set theFilename to item 1 of argv
		set theLines to every paragraph of (do shell script ("cat " & theFilename))
		set theCalendarStr to item 1 of theLines -- FIXME: Change this to use the uid of the calendar instead
		set theSubject to item 2 of theLines
		set theMessageURL to item 3 of theLines
		set theDate to item 4 of theLines
		if theDate = ""
			beep
			return
		end if

		-- set Applescript's text item delimiters to "\n"
		-- set theNote to items 4 thru end of theLines as string
		set theNote to ""

		set theStartDate to getDateFromString(theDate)

		tell application id "com.apple.iCal" to tell (first calendar whose name is theCalendarStr)
			set theEvent to make new event with properties {start date:theStartDate, description:theNote, summary:theSubject, url:theMessageURL}
			-- reload calendars
			show theEvent
		end tell
	on error errMsg number eNum
		tell application id "com.apple.iCal"
			activate
			display alert eNum message errMsg
		end tell
	end try

	-- Other potentially useful properties:
	--  allday event (boolean) : True if the event is an all-day event
	--  recurrence (text) : The iCalendar (RFC 2445) string describing the event recurrence, if defined
	--  excluded dates (list of date) : The exception dates.
	--  status (cancelled/confirmed/none/tentative) : The event status.
	--  location (text) : This is the event location.
end run
